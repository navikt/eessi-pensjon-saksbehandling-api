buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.2.2.RELEASE'
        springfoxVersion = '2.9.2'
        jacksonVersion = '2.10.2'
        mockitoVersion = '3.2.4'
        oidcTokenSupportVersion = '0.2.18'
        logstashLogbackVersion = '6.3'
        pac4jVersion = '3.2.0'
        cxfVersion = '3.3.4'
        personV3Version = '3.6.1'
        openhtmltopdfVersion = '1.0.0'
    }
    repositories {

        if (project.hasProperty('github')) {
            mavenCentral()

            maven {
                url "https://maven.pkg.github.com/navikt/maven-release"
                credentials {
                    username = "token"
                    password = "$System.env.GITHUB_TOKEN"
                }
            }

            maven {
                credentials {
                    username = "token"
                    password = "$System.env.GITHUB_TOKEN"
                }

                setUrl("https://maven.pkg.github.com/navikt/tjenestespesifikasjoner")
            }
        } else {
            maven {
                url "https://repo.adeo.no/repository/maven-central"
            }
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id "org.sonarqube" version "2.8"
    id 'jacoco'
    id 'com.adarshr.test-logger' version '2.0.0'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    systemProperty "file.encoding", "utf-8"
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}


group = 'no.nav.eessi'
version = '0.0.1'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
jar {
    baseName = 'eessi-fagmodul-frontend'
}

dependencies {

    // Tjenestespesifikasjoner
    if (project.hasProperty('github')) {
        compile("no.nav.tjenestespesifikasjoner:person-v3-tjenestespesifikasjon:1.2020.01.30-14.36-cdf257baea96")
        implementation("no.nav.tjenestespesifikasjoner:nav-fim-behandleJournal-v3-tjenestespesifikasjon:1.2020.01.30-14.36-cdf257baea96")
    } else {
        implementation("no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:${personV3Version}")
//        implementation('no.nav.meldinger.virksomhet:nav-virksomhet-varsel-v1-meldingsdefinisjon:1.1.1:jaxb')
        implementation("no.nav.tjenester.fim:nav-fim-behandleJournal-v3-tjenestespesifikasjon:3.0.2:jaxws")
    }

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation('org.springframework.boot:spring-boot-starter-websocket')
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'junit'
        exclude module: "mockito-core"
    }
    implementation("org.springframework:spring-jms")
    // MQ
    implementation("com.ibm.mq:com.ibm.mq.allclient:9.1.4.0")

    // Messaging
    implementation("org.springframework:spring-websocket")
    implementation("org.springframework:spring-messaging")

    // Kafka
    implementation("org.springframework.kafka:spring-kafka")

    // Apache CXF
    implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")

    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}")
    implementation("commons-io:commons-io:2.6")

    // Kotlin
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    // Swagger
    implementation("io.springfox:springfox-swagger2:${springfoxVersion}")
    implementation("io.springfox:springfox-swagger-ui:${springfoxVersion}")

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

    // auditlogger syslog
    implementation("com.papertrailapp:logback-syslog4j:1.0.0")
    implementation("org.syslog4j:syslog4j:0.9.30")

    // PDF box
    implementation("org.apache.pdfbox:pdfbox:2.0.18")
    implementation("com.openhtmltopdf:openhtmltopdf-core:${openhtmltopdfVersion}")
    implementation("com.openhtmltopdf:openhtmltopdf-pdfbox:${openhtmltopdfVersion}")
    implementation("com.openhtmltopdf:openhtmltopdf-jsoup-dom-converter:${openhtmltopdfVersion}") // deprecated

    // HTML-parser/manipulator
    implementation('org.jsoup:jsoup:1.12.1')

    // Amazon S3
    implementation("com.amazonaws:aws-java-sdk-s3:1.11.703")

    // Micrometer
    implementation("io.micrometer:micrometer-registry-prometheus")
    
    // OIDC
    implementation("no.nav.security:oidc-spring-support:${oidcTokenSupportVersion}")
    implementation("no.nav.security:oidc-test-support:${oidcTokenSupportVersion}")
    implementation("org.pac4j:spring-webmvc-pac4j:${pac4jVersion}")
    implementation("org.pac4j:pac4j-oidc:${pac4jVersion}")
    implementation("org.pac4j:pac4j-core:${pac4jVersion}")

    //mock
    testImplementation("no.nav.security:oidc-test-support:${oidcTokenSupportVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:5.5.2")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
    testImplementation("io.findify:s3mock_2.12:0.2.5")
    testImplementation "io.mockk:mockk:1.9.3"

    // Architecture tests
    testImplementation 'com.tngtech.archunit:archunit:0.12.0'

    // Allows for variable expiration-date of each cache-entry
    implementation("net.jodah:expiringmap:0.5.9")

    // Apache CXF
    implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")
    implementation('org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1')

    // LDAP
    implementation("org.springframework.ldap:spring-ldap-core")
    implementation("org.springframework.security:spring-security-ldap")
    implementation("com.unboundid:unboundid-ldapsdk")
}

repositories {

    if (project.hasProperty('github')) {
        mavenCentral()

        maven {
            url "https://maven.pkg.github.com/navikt/maven-release"
            credentials {
                username = "token"
                password = "$System.env.GITHUB_TOKEN"
            }
        }

        maven {
            credentials {
                username = "token"
                password = "$System.env.GITHUB_TOKEN"
            }

            setUrl("https://maven.pkg.github.com/navikt/tjenestespesifikasjoner")
        }
    } else {
        maven {
            url "https://repo.adeo.no/repository/maven-central"
        }
        maven {
            url "https://repo.adeo.no/repository/maven-releases"
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

//
//bootRun {
//    String activeProfile = System.properties['spring.profiles.active']
//    systemProperty "spring.profiles.active", activeProfile
//}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn jacocoTestReport

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Not a real release')
                }
            }
        }
    }
    revision = 'release'
}
