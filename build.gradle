buildscript {
    ext {
        kotlinVersion = '1.7.10'
        springBootVersion = '2.7.3'
        springKafkaVersion = '2.9.0'
        jacksonVersion = '2.13.3'
        tokenSupportVersion = '2.1.3'
        logstashLogbackVersion = '7.2'
        pac4jVersion = '5.4.5'
        cxfVersion = '3.5.3'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "org.owasp.dependencycheck" version "7.1.1"
    id 'com.adarshr.test-logger' version '3.2.0'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    systemProperty "file.encoding", "utf-8"
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

group = 'no.nav.eessi'

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17): "Java 17 or newer is required"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
        allWarningsAsErrors = true
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
        allWarningsAsErrors = true
    }
}

dependencies {

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-websocket")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: "junit-vintage-engine"
    }

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:5.3.22"))

    implementation("org.springframework:spring-jms")

    // Messaging
    implementation("org.springframework:spring-websocket")
    implementation("org.springframework:spring-messaging")

    // Kafka
    implementation("org.springframework.kafka:spring-kafka:$springKafkaVersion")

    // Apache CXF
    implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")

    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}")

    // Kotlin
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}")

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion"))
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'
    implementation("no.nav.eessi.pensjon:ep-logging:1.0.34")

    //GCP
    implementation(platform("com.google.cloud:libraries-bom:26.1.0"))
    implementation("com.google.cloud:google-cloud-storage")

    // Micrometer
    implementation("io.micrometer:micrometer-registry-prometheus")
    implementation("no.nav.eessi.pensjon:ep-metrics:0.5.8")
    implementation("no.nav.eessi.pensjon:ep-eux:1.3.7")
    //auth
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-test-support:2.0.5")

    implementation("org.pac4j:pac4j-oidc:${pac4jVersion}")
    implementation("org.pac4j:pac4j-core:${pac4jVersion}")

    testImplementation(platform("org.junit:junit-bom:5.9.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    //mock
    testImplementation('io.mockk:mockk:1.12.5')
    testImplementation('com.ninja-squad:springmockk:3.1.1')

    // Architecture tests
    testImplementation 'com.tngtech.archunit:archunit:0.23.1'

    // Apache CXF
    implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")
    implementation('org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1')

    // Kafka
    testImplementation "org.springframework.kafka:spring-kafka-test:$springKafkaVersion"

    // LDAP
    implementation("org.springframework.ldap:spring-ldap-core")
    implementation("org.springframework.security:spring-security-ldap")
    implementation("com.unboundid:unboundid-ldapsdk")
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]


repositories {
    mavenCentral()

    String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
    if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")

    maven {
        url = uri("https://maven.pkg.github.com/navikt/maven-release")
        credentials {
            username = "token"
            password = token
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
    }
    revision = 'release'
}
